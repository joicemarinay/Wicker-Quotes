def Group(Closure closure) {
    closure.delegate = dependencies
    return closure
}

/**
 All groups below for easy copy/pasting:

 libraries()
 kotlin()
 archiComponents()
 dagger()
 rx()
 timber()
 ui()
 androidTesting()
 testing()
 **/
ext {

    /**
     * Libraries
     */
    libraries = Group {
        implementation deps.libs
    }

    /**
     * Kotlin
     */
    kotlin = Group {
        implementation deps.kotlin.lib
    }

    /**
     * Android Architecture Components
     */
    //TODO separate group for android components for feature modules
    archiComponents = Group {
        //For both ViewModel and LiveData
        implementation deps.archiComponents.extensions
        kapt deps.archiComponents.compiler
    }

    /**
     * Dagger
     */
    dagger = Group {
        implementation deps.dagger.dagger
        kapt deps.dagger.compiler
    }

    /**
     * Timber (for logging)
     */
    timber = Group {
        implementation deps.timber
    }

    /**
     * Room
     */
    room = Group {
        implementation deps.archiComponents.roomRuntime
        implementation deps.archiComponents.roomRxJava
        kapt deps.archiComponents.roomCompiler
    }

    /**
     * Rx
     */
    rx = Group {
        implementation deps.rx.android
        implementation deps.rx.java
    }

    /**
     * UI
     */
    ui = Group {
        implementation deps.androidSupport.appCompat
        implementation deps.androidSupport.constraint
        implementation deps.androidSupport.design
        implementation deps.androidSupport.recyclerview
        implementation deps.androidSupport.supportCompat
    }

    /**
     * Android Testing
     */
    androidTesting = Group {
        androidTestImplementation(deps.androidTest.runner)
        androidTestImplementation(deps.androidTest.espresso)

        //Dagger
        kaptAndroidTest deps.dagger.compiler
    }

    /**
     * Testing
     *
     * Note: Put test modules of other dependencies here
     */
    testing = Group {
        testImplementation(deps.jUnit) { exclude group: 'org.hamcrest' }
        testImplementation deps.archiComponents.testing

        //Dagger
        kaptTest deps.dagger.compiler

        //Mockito
        testImplementation deps.mockito.core
        testImplementation deps.mockito.inline
    }

    //Can add groups for Dagger, debugging, network, Rx, Android Archi Components
}
