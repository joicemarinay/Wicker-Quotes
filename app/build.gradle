apply plugin: 'com.android.application'
apply from: '../config-android.gradle'
apply from: '../dependencies-groups.gradle'

/**
 * App name and version
 */
enum BuildType {
    DEBUG,
    RELEASE
}

static def getAppName(BuildType buildType) {
    String APP_NAME = "Wicker"
    switch (buildType) {
        case BuildType.DEBUG:
            return APP_NAME + " - " + BuildType.DEBUG.name()
        case BuildType.RELEASE:
            return APP_NAME
    }
}

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0

/**
 * Android Configs
 */
android {
    defaultConfig {
        applicationId "io.rcm.wicker"
        versionCode versionMajor * 1000 + versionMinor * 100 + versionPatch * 10
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            resValue "string", "app_name", getAppName(BuildType.DEBUG)
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", getAppName(BuildType.RELEASE)
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

/**
 * Dependencies
 */
dependencies {
    libraries()
    kotlin()
    archiComponents()
    dagger()
    rx()
    ui()
    androidTesting()
    testing()

    implementation project(path: ':base')
    implementation project(path: ':quotes')
}
